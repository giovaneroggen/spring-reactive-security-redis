buildscript {
    ext {
        springBootVersion = '2.1.1.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'http://repo.spring.io/release'
        }
    }
    dependencies {
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.3")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    ext {
        springBootVersion = '2.1.1.RELEASE'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
           url "https://dl.bintray.com/kptfh/feign-reactive"
        }
    }

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"
    apply plugin: "jacoco"
    group 'br.com'
    version = '2.1.1.RELEASE'
    sourceCompatibility = JavaVersion.VERSION_1_10

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Finchley.RELEASE"
        }
    }

    dependencies {
        compile("org.projectlombok:lombok:1.18.2")
        compile("javax.annotation:javax.annotation-api:1.3.1")
    }

    ext.clientWithContract = { String contract ->
        dependencies {
            compile project(":${contract}")
            compileOnly("org.springframework.boot:spring-boot-starter-webflux")
            compile 'io.github.reactivefeign:feign-reactor-cloud:1.0.0'
        }
    }

    ext.serviceWithContract = { String contract ->
        apply plugin: 'org.springframework.boot'
        dependencies {
            compile project(":${contract}")
            compile("org.springframework.boot:spring-boot-starter-webflux")
        }
    }
}